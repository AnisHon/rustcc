WHITESPACE    = _{ " " | "\t" | "\r" | "\n" | COMMENT }
COMMENT       = _{ block_comment | line_comment }
block_comment =  { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
line_comment  =  { "//" ~ (!("\n" | "\r") ~ ANY)* ~ ("\n" | "\r" | EOI) }

file = { SOI ~ decl_code? ~ decls ~ "%%" ~ rules ~ "%%" ~ user_code? ~ EOI }

// 声明代码块（%{%} 包裹的内容）
decl_code = { "%{" ~ (!"%}" ~ ANY)* ~ "%}" }

decls      = { decl* }
decl       = { token_decl | assoc_decl | type_decl | param_decl }

param_decl = { "%param" ~ "{" ~ param_content ~ "}"}
param_content = { (!"}" ~ ANY)+ }

token_decl = { "%token" ~ ident+ }

assoc_type = { "left" | "right" | "nonassoc" }
assoc_decl = { ("%" ~ assoc_type) ~ symbol+ }

type_decl  = { "%type" ~ ident }

rules                = { rule_decl* }
// 允许 0 或多个规则
rule_decl            = { ident ~ ":" ~ production_with_prec ~ ("|" ~ production_with_prec)* ~ ";" }
production_with_prec = { production ~ prec_directive? }
prec_directive       = { "%prec" ~ prec_type }
prec_type            = { assoc_type }

production       =  { (symbol | action)* }
symbol           =  { quoted_symbol | unquoted_symbol }
quoted_symbol   =  { "\'" ~ (!"\'" ~ (ESCAPED_CHAR | ASCII_PRINTABLE))* ~ "\'" }
ESCAPED_CHAR     =  { "\\" ~ ("\'" | "\\" | "n" | "r" | "t") }
unquoted_symbol = @{ (!("\'" | ":" | "|" | ";" | "{" | "}") ~ ASCII_ALPHANUMERIC | "_")+ }
ASCII_PRINTABLE  =  { ASCII_ALPHANUMERIC | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~" }

action         = { "{" ~ action_content ~ "}" }
action_content = { (!"}" ~ (action | ASCII))* }

user_code = { (!EOI ~ ASCII)* }

ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }